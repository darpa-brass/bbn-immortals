import groovy.json.JsonSlurper
import mil.darpa.immortals.config.ImmortalsConfig

import java.nio.file.Paths

buildscript {
    def IMMORTALS_REPO = new JsonSlurper().parseText(Paths.get(getBuildscript().getSourceFile().parent)
            .resolve('immortals_config.json').toFile().text).globals.immortalsRepo
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url IMMORTALS_REPO
        }
        maven {
            url 'https://repo.gradle.org/gradle/libs-releases'
        }
    }
    dependencies {
        classpath 'mil.darpa.immortals:ImmortalsGradle:+'
        classpath 'mil.darpa.immortals:ImmortalsConfig:+'
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

repositories {
    mavenCentral()
    maven {
        url ImmortalsConfig.instance.globals.immortalsRepo
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'dependencies.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8
group "com.bbn.ataklite"
version "2.0-LOCAL"

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

publishing {
    publications {
        JavaLib(MavenPublication) {
            from project.components.java
        }
    }
    repositories {
        maven {
            url ImmortalsConfig.instance.globals.immortalsRepo
        }
    }
}

task appJar(type: Jar) {
    manifest {
        attributes "Implementation-Title": "CLITAK",
                "Main-Class": "com.bbn.ataklite.Main"
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    destinationDir = file('./')
    archiveName = "CLITAK.jar"
}

jar {
    manifest {
        attributes "Implementation-Title": "CLITAK-lib"
    }
}

jar.finalizedBy(publish)
jar.finalizedBy(appJar)

clean {
    delete fileTree(dir: './', includes: ['build', 'CLITAK.jar'])
}

dependencies {
    compile 'mil.darpa.immortals.datatypes:CotData:+'
    compile 'mil.darpa.immortals.datatypes:Coordinates:+'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'mil.darpa.immortals:core:+'
    compile 'com.securboration:immortals-adsl-generate:+'
    compile 'mil.darpa.immortals.datatypes:Coordinates:+'
}
