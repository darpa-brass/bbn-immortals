apply plugin: mil.darpa.immortals.ShadowJavaModule

repositories {
    jcenter()
    maven {
        url 'http://download.osgeo.org/webdav/geotools/'
    }
    maven {
        url 'http://repo.boundlessgeo.com/main'
    }
    mavenCentral()
}

// This would work if http://central.maven.org/maven2/javax/media/jai_core/1.1.3/ wasn't missing the jar.
// But since it is, this redirects it to a matching package in jcenter
configurations.all {
    resolutionStrategy.eachDependency {
        DependencyResolveDetails details ->
            if (details.requested.name == 'jai_core') {
                details.useTarget group: 'javax.media', name: 'jai-core', version: details.requested.version
            }
    }
}

jar {
    manifest {
        attributes "Implementation-Title": "ElevationApi",
                "Main-Class": "mil.darpa.immortals.dfus.ElevationApi"
    }
}

dependencies {
    compile project(':shared:modules:core')
    compile('org.geotools:gt-geotiff:18.1') {
        exclude group: 'xerces', module: 'xercesImpl'
    }
    compile 'xerces:xercesImpl:2.11.0'
    
    compile 'org.geotools:gt-epsg-wkt:18.1'
}
